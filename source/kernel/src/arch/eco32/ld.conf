OUTPUT_FORMAT("elf32-eco32")
ENTRY(start)

PHDRS
{
	text	PT_LOAD;
	data	PT_LOAD;
	bss		PT_LOAD;
}

SECTIONS
{
	. = 0xC0000000;

	.text : AT(ADDR(.text) - 0xC0000000)
	{
		_btext = .;
		*(.text*)
	} : text

	.rodata : AT(ADDR(.rodata) - 0xC0000000)
	{
		*(.rodata*)
	} : text

    .init_array : AT (ADDR (.init_array) - 0xC0000000)
    {
        PROVIDE (CTORS_BEGIN = .);
        KEEP (*(.init_array .ctors))
        PROVIDE (CTORS_END = .);
    } : data

	.data ALIGN (4096) : AT(ADDR(.data) - 0xC0000000)
	{
		*(.data)
		*(.data.*)
	} : data

	.bss ALIGN (4096) : AT(ADDR(.bss) - 0xC0000000)
	{
		*(COMMON*)
		*(.bss*)
		_ebss = .;
	} : bss
}
